plugins {
    id 'maven-publish'
}

repositories {
    jcenter()
    maven { url = "https://oss.sonatype.org/content/repositories/releases/" }
}

dependencies {
    modCompile "de.javagl:obj:${javagl_version}"
    include "de.javagl:obj:${javagl_version}"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives sourcesJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'FOML'

            afterEvaluate {
                artifact(remapJar) {
                    builtBy remapJar
                }
            }
            artifact sourcesJar
            pom {
                name = "FOML"
                url = "https://github.com/OnyxStudios/FOML"
                licenses {
                    license {
                        name = "MIT License"
                        url = "https://github.com/OnyxStudios/FOML/blob/master/LICENSE.md"
                    }
                }
                scm {
                    url = "https://github.com/OnyxStudios/FOML"
                    connection = "scm:git:git://github.com/OnyxStudios/FOML.git"
                    developerConnection = "scm:git:git@github.com:OnyxStudios/FOML.git"
                }
                issueManagement {
                    system = "github"
                    url = "https://github.com/OnyxStudios/FOML/issues"
                }
            }
        }
    }

    repositories {
        if (project.hasProperty("maven_user") && project.hasProperty("maven_pass")) {
            maven {
                name = "OnyxMaven"
                url = findProperty("maven_url")
                credentials {
                    username = findProperty("maven_user")
                    password = findProperty("maven_pass")
                }
                authentication {
                    basic(BasicAuthentication)
                }
            }
        }
    }
}
tasks.publish.dependsOn build
